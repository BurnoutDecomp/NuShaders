#include "../Include/Transform.fxh"
#define SHADOW_APPLY_FADE_ROAD
#define SHADOW_ROAD_X360_USER
#include "../Include/Shadow.fxh"
#include "../Include/Fog.fxh"
#include "../Include/Irradiance.fxh"
#include "../Include/DepthEncode.fxh"
#ifdef D_OREN_NAYAR
#include "../Include/OrenNayar.fxh"
#endif
#ifdef D_VERSION_16
bool gbDisableAniso : register(b0);
#endif
#ifdef D_PLATFORM_X360
float3   KeyLightDirection : register(c24)
#else
float3   KeyLightDirection
#endif
<
 string scope = "global";
>;
#ifdef D_PLATFORM_X360
float3   ViewPosition : register(c25)
#else
float3   ViewPosition
#endif
<
 string scope = "global";
>;
float3   KeyLightColour
<
 string scope = "global";
>;
float4x4  worldViewProj : WorldViewProjection
<
 string scope = "object";
>;
#ifdef D_PLATFORM_X360
float4x4  world : register(c26)
#else
float4x4  world
#endif
 : World
<
 string scope = "object";
>;
float4 materialDiffuse
<
 string scope = "material";
 string purpose = "none";
    string UIWidget = "rgba";
> = {1.0f, 1.0f, 1.0f, 1.0f};
texture2D baseMap : diffuse
<
 string scope   = "material";
 string purpose = "none";
>;
texture2D detailMap
<
 string scope   = "material";
 string purpose = "none";
>;
sampler2D baseMapSampler : register(s0)
<
 string scope   = "material";
 string purpose = "none";
> = sampler_state
{
 Texture = <baseMap>;
 MinFilter = Linear;
 MagFilter = Linear;
 MipFilter = Linear;
};
sampler2D detailMapSampler : register(s1)
<
 string scope   = "material";
 string purpose = "none";
> = sampler_state
{
 Texture = <detailMap>;
 MinFilter = Linear;
 MagFilter = Linear;
 MipFilter = Linear;
};
struct vertexInput {
    float3 position    : POSITION;
    float3 normal    : NORMAL;
    float2 texCoordDiffuse  : TEXCOORD0;
};
struct vertexOutput {
    float4 hPosition    : POSITION;
    float3 texCoordDiffuseAndFog : TEXCOORD0;
    float2 texCoordDetailMap      : TEXCOORD1;
 SHADOWMAP_INTERPOLATORS2( 2,3 )
    float4 IndirectColourAndKey  : TEXCOORD4;
#ifdef D_MRT
    float2 hPositionDepthCopy       : TEXCOORD5;
#endif
#if (defined(D_OREN_NAYAR) || defined(D_GGX_SPECULAR)) && !defined(D_ROAD_X360)
    // D_ROAD_X360: the shipped X360 permutation (VS 1648EA1C / PS F59D9209) has no
    // Oren-Nayar path -- no WorldNormal/ViewDirection interpolators exist.
    float3 WorldNormal              : TEXCOORD6;
    float3 ViewDirection            : TEXCOORD7;
#endif
};
struct vertexOutputLod1 {
    float4 hPosition    : POSITION;
    float3 texCoordDiffuseAndFog : TEXCOORD0;
 float4 LightSpacePosFar   : TEXCOORD2;
    float4 IndirectColourAndKey  : TEXCOORD3;
};
vertexOutput VS_Main(vertexInput IN
      ) 
{
    vertexOutput OUT;
     float3 WorldSpacePosition = mul( float4( IN.position, 1.0f ), world ).xyz;           
    float3 WorldSpaceNormal  = normalize( mul( IN.normal, (float3x3)world ) );             
    OUT.hPosition    = TransformWorldToProjection( WorldSpacePosition );
#ifdef D_MRT
    OUT.hPositionDepthCopy = OUT.hPosition.zw;
#endif
    OUT.texCoordDiffuseAndFog.xy = IN.texCoordDiffuse;
    OUT.texCoordDetailMap.xy = IN.position.xz;
    OUT.IndirectColourAndKey.xyz = ComputeIrradianceFast( WorldSpaceNormal );
    OUT.IndirectColourAndKey.w = dot( WorldSpaceNormal, -KeyLightDirection ); 
    float3 lEyeToVertex    = ViewPosition.xyz - WorldSpacePosition;
    OUT.texCoordDiffuseAndFog.z = CalculateScattering( length( lEyeToVertex ) );
    CALC_SHADOWMAP_INTERPOLATORS3( WorldSpacePosition, OUT.hPosition.w );
#if (defined(D_OREN_NAYAR) || defined(D_GGX_SPECULAR)) && !defined(D_ROAD_X360)
    OUT.WorldNormal = WorldSpaceNormal;
    OUT.ViewDirection = normalize( ViewPosition.xyz - WorldSpacePosition );
#endif
    return OUT;
}
#ifdef D_MRT
void PS_Main( in  vertexOutput IN,
              out float4 oColour0 : COLOR0,
              out float4 oColour1 : COLOR1 )
#else
float4 PS_Main( vertexOutput IN ) : COLOR
#endif
{
    float4 lUV;
    lUV.xy = IN.texCoordDiffuseAndFog.xy;
    lUV.zw = IN.texCoordDetailMap;
#ifdef D_VERSION_16
 float  detailsT;
 if (gbDisableAniso) { detailsT = tex2Dbias(detailMapSampler, float4(lUV.zw, 0, 0)).r; }
 else { detailsT = tex2D(detailMapSampler, lUV.zw).r; }
#else
 float  detailsT = tex2D(detailMapSampler, lUV.zw).r;
#endif
 float3 baseT    = tex2D(baseMapSampler, lUV.xy).rgb;
 detailsT *= (float)2.0;
 float3 diffuseTexture = baseT * detailsT;
 float lShadowModulation = CALC_SHADOW_FACTOR_3( (float)IN.IndirectColourAndKey.w );
#if defined(D_OREN_NAYAR) && !defined(D_ROAD_X360)
    float  lDirectLightFactor   = ComputeOrenNayarDiffuse( normalize(IN.WorldNormal), (float3)-KeyLightDirection, normalize(IN.ViewDirection), 0.1 ) * lShadowModulation;
#else
    // X360 (PS F59D9209): plain saturate(vertex N.L) * shadow
    float  lDirectLightFactor   = saturate( (float)IN.IndirectColourAndKey.w ) * lShadowModulation;
#endif
    float3 lDirectLightColour   = float3( KeyLightColour );
    float3 lIndirectLightColour = float3( IN.IndirectColourAndKey.xyz );
#ifdef D_ROAD_X360
    // X360 (PS F59D9209): cross-fade indirect -> key light (the PC add double-counts ambient in sun)
    float3 lLightColour         = lerp( lIndirectLightColour, lDirectLightColour, lDirectLightFactor ) * float3(materialDiffuse.xyz);
#else
    float3 lLightColour         = ( lIndirectLightColour + lDirectLightColour * lDirectLightFactor ) * float3(materialDiffuse.xyz);
#endif
    float3 lFinalColour         = (diffuseTexture * lLightColour);
    lFinalColour = lerp( lFinalColour.rgb, float3(FogColourPlusWhiteLevel.rgb), float(IN.texCoordDiffuseAndFog.z) );
#ifdef D_MRT
    oColour0 = float4(lFinalColour, 1);
    float lfDepth = ( IN.hPositionDepthCopy.x / IN.hPositionDepthCopy.y );
    oColour1 = ConvertDepthToARGB( lfDepth );
#else
    return float4(lFinalColour, 1);
#endif
}
technique Default
<
>
{
    pass p0
    {  
  VertexShader = compile vs_3_0 VS_Main();
  PixelShader  = compile ps_3_0 PS_Main();
    }
}
struct vertexInputZOnly {
    float3 position    : POSITION;
};
struct vertexOutputZOnly {
    float4 hPosition   : POSITION;
#ifdef D_MSAA_ENABLED
    float2 hPositionDepthCopy   : TEXCOORD0;
#endif
};
vertexOutputZOnly VS_Main_ZOnly( vertexInputZOnly IN
        ) 
{ 
    vertexOutputZOnly OUT;
     float3 WorldSpacePosition = mul( float4( IN.position, 1.0f ), world ).xyz;           
    OUT.hPosition    = TransformWorldToProjection( WorldSpacePosition );
#ifdef D_MSAA_ENABLED
    OUT.hPositionDepthCopy = OUT.hPosition.zw;
#endif
    return OUT;
}
float4 PS_Main_ZOnly( vertexOutputZOnly IN ): COLOR
{
#ifdef D_MSAA_ENABLED
#ifdef D_ADD_STENCIL
    float lfDepth = ( IN.hPositionDepthCopy.x / IN.hPositionDepthCopy.y ) + 2.0f;
#else
    float lfDepth = ( IN.hPositionDepthCopy.x / IN.hPositionDepthCopy.y );
#endif
    return lfDepth.xxxx;
#else
    return float4(1,1,1,1);
#endif
}
technique ZOnlyOpaqueSingleSided
<
  string sharedName="ZOnlyOpaqueSingleSided";
>
{
    pass p0
    {  
        CullMode = cw;
#ifndef D_MSAA_ENABLED
        ColorWriteEnable = 0;
#endif
  VertexShader = compile vs_3_0 VS_Main_ZOnly();
  PixelShader  = compile ps_3_0 PS_Main_ZOnly();
    }
}



