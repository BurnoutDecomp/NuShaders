#include "../Include/Transform.fxh"
#include "../Include/Shadow.fxh"
#include "../Include/Fog.fxh"
#include "../Include/Irradiance.fxh"
#include "../Include/DepthEncode.fxh"
#ifdef D_OREN_NAYAR
#include "../Include/OrenNayar.fxh"
#endif
#ifdef D_GGX_SPECULAR
#include "../Include/GGX.fxh"
#endif
float3   ViewPosition : cameraposition
<
 string scope = "global";
>;
float3   KeyLightDirection
<
 string scope = "global";
>;
float3   KeyLightSpecularColour
<
 string scope = "global";
>;
float3   KeyLightColour
<
 string scope = "global";
>;
float4x4  worldViewProj : WorldViewProjection
<
 string scope = "object";
>;
float4x4  world : World
<
 string scope = "object";
>;
float4 materialDiffuse
<
 string scope = "material";
    string UIWidget = "rgba";
> = {1.0f, 1.0f, 1.0f, 1.0f};
float SpecularPower
<
 string scope = "material";
    string UIWidget = "slider";
    float UIMin = 1.0;
    float UIMax = 100.0;
    float UIStep = 1.0;
    string UIName = "specular power";
> = 12.0;
float Specularity
<
 string scope = "material";
    string UIWidget = "slider";
    float UIMin = 1.0;
    float UIMax = 10.0;
    float UIStep = 0.1;
    string UIName = "Specularity";
> = 1.5;
float SpecularPowerReflection
<
 string scope = "material";
    string UIWidget = "slider";
    float UIMin = 1.0;
    float UIMax = 4000.0;
    float UIStep = 1.0;
    string UIName = "specular power on reflection";
> = 2000.0;
float SpecularityReflection
<
 string scope = "material";
    string UIWidget = "slider";
    float UIMin = 1.0;
    float UIMax = 10.0;
    float UIStep = 0.1;
    string UIName = "Specularity on reflection";
> = 5.0;
texture2D DiffuseTexture : diffuse
<
 string scope = "material";
 string purpose = "none";
>;
texture2D SpecularTexture
<
 string scope = "material";
 string purpose = "none";
>;
textureCUBE ReflectionTexture
<
 string scope = "material";
 string purpose = "none";
>;
sampler2D DiffuseTextureSampler : register(s0) 
<
 string scope = "material";
 string purpose = "none";
> = sampler_state
{
 Texture = <DiffuseTexture>;
 MinFilter = Linear;
 MagFilter = Linear;
 MipFilter = Linear;
};
sampler2D SpecularTextureSampler : register(s1) 
<
 string scope = "material";
 string purpose = "none";
> = sampler_state
{
 Texture = <SpecularTexture>;
 MinFilter = Linear;
 MagFilter = Linear;
 MipFilter = Linear;
};
samplerCUBE ReflectionTextureSampler : register(s2)
<
 string scope = "material";
 string purpose = "none";
> = sampler_state
{
 Texture = <ReflectionTexture>;
 MinFilter = Linear;
 MagFilter = Linear;
 MipFilter = Linear;
 AddressU = clamp;
 AddressV = clamp;
};
struct vertexInput {
    float3 position    : POSITION;
    float3 normal    : NORMAL;
    float2 texCoordDiffuse  : TEXCOORD0;
};
struct vertexOutput {
    float4 hPosition    : POSITION;
    float2 texCoordDiffuse   : TEXCOORD0;
    SHADOWMAP_INTERPOLATORS2( 1,2 )    
    float4 IndirectColourAndKey  : TEXCOORD3;
 float4 ReflectionVectorAndFog : TEXCOORD4;
#ifdef D_MRT
    float2 hPositionDepthCopy       : TEXCOORD6;
#endif
#if defined(D_OREN_NAYAR) || defined(D_GGX_SPECULAR)
    float3 WorldNormal              : TEXCOORD7;
    float3 ViewDirection            : TEXCOORD5;
#endif
};
struct vertexOutputLod1 {
    float4 hPosition    : POSITION;
    float2 texCoordDiffuse   : TEXCOORD0;
 float4 LightSpacePosFar   : TEXCOORD1;
    float4 IndirectColourAndKey  : TEXCOORD2;
 float4 ReflectionVectorAndFog : TEXCOORD3;
#ifdef D_MRT
    float2 hPositionDepthCopy       : TEXCOORD5;
#endif
};
vertexOutput VS_Main( vertexInput IN
      ) 
{ 
    vertexOutput OUT;
     float3 WorldSpacePosition = mul( float4( IN.position, 1.0f ), world ).xyz;           
    float3 WorldSpaceNormal  = normalize( mul( IN.normal, (float3x3)world ) );             
    OUT.hPosition    = TransformWorldToProjection( WorldSpacePosition );
#ifdef D_MRT
    OUT.hPositionDepthCopy = OUT.hPosition.zw;
#endif
    OUT.texCoordDiffuse = IN.texCoordDiffuse;
    CALC_SHADOWMAP_INTERPOLATORS3( WorldSpacePosition, OUT.hPosition.w );
    OUT.IndirectColourAndKey.xyz = ComputeIrradianceFast( WorldSpaceNormal );
    OUT.IndirectColourAndKey.w   = dot( WorldSpaceNormal, -KeyLightDirection ); 
    float3 lVertexToEye = ViewPosition.xyz - WorldSpacePosition;
    OUT.ReflectionVectorAndFog.xyz = ( WorldSpaceNormal * ( 2.0f * dot( lVertexToEye, WorldSpaceNormal ) ) ) - lVertexToEye;
    OUT.ReflectionVectorAndFog.w = CalculateScattering( length( lVertexToEye ) );
#if defined(D_OREN_NAYAR) || defined(D_GGX_SPECULAR)
    OUT.WorldNormal = WorldSpaceNormal;
    OUT.ViewDirection = normalize( ViewPosition.xyz - WorldSpacePosition );
#endif
    return OUT;
}
#ifdef D_MRT
void PS_Main( in  vertexOutput IN,
              out float4 oColour0 : COLOR0,
              out float4 oColour1 : COLOR1 )
#else
float4 PS_Main( vertexOutput IN ) : COLOR
#endif
{
 float3 diffuseTexture  = tex2D( DiffuseTextureSampler, IN.texCoordDiffuse ).rgb;
 float2 specularTexture = tex2D( SpecularTextureSampler, IN.texCoordDiffuse ).rg;
    float3 lLightDirection   = (float3)-KeyLightDirection;
    float3 lReflectionVector = (float3)IN.ReflectionVectorAndFog.xyz;
    float  lhSpecularPower = lerp( (float)SpecularPower, (float)SpecularPowerReflection, specularTexture.x );
    float  lhSpecularity   = lerp( (float)Specularity,   (float)SpecularityReflection,   specularTexture.x );
    float3 lReflectionNormalised = normalize( (float3)lReflectionVector );
    float  lrRdotL = saturate( dot( lReflectionNormalised, lLightDirection ) );
#ifdef D_GGX_SPECULAR
    float  lGGXRoughness = sqrt( 2.0 / ( lhSpecularPower + 2.0 ) );
    float3 lSpecularColour = float3(KeyLightSpecularColour) * ( lhSpecularity * ComputeGGXSpecular( normalize(IN.WorldNormal), (float3)-KeyLightDirection, normalize(IN.ViewDirection), lGGXRoughness, 0.04 ) );
#else
    float3 lSpecularColour = float3(KeyLightSpecularColour) * ( lhSpecularity * pow( lrRdotL, lhSpecularPower ) );
#endif
    lSpecularColour *= specularTexture.y;
 float lShadowModulation = CALC_SHADOW_FACTOR_3( (float)IN.IndirectColourAndKey.w );
 float3 reflectionTexture    = texCUBE(ReflectionTextureSampler, lReflectionVector).rgb;
 float  reflectionIntensity  = (float)FogColourPlusWhiteLevel.w;
 float3 reflectionColour     = reflectionTexture * reflectionIntensity;
    float  lhReflectionBlend    = specularTexture.x;
#ifdef D_OREN_NAYAR
    float  lDirectLightFactor   = ComputeOrenNayarDiffuseFromSpecMap( normalize(IN.WorldNormal), (float3)-KeyLightDirection, normalize(IN.ViewDirection), specularTexture ) * lShadowModulation;
#else
    float  lDirectLightFactor   = saturate( (float)IN.IndirectColourAndKey.w ) * lShadowModulation;
#endif
    float3 lDirectLightColour   = float3( KeyLightColour );
    float3 lIndirectLightColour = float3( IN.IndirectColourAndKey.xyz );
    float3 lLightColour         = ( lIndirectLightColour + lDirectLightColour * lDirectLightFactor ) * float3(materialDiffuse.xyz);
    float3 lFinalColour         = lerp( (diffuseTexture * lLightColour), reflectionColour, lhReflectionBlend) + (lSpecularColour * lShadowModulation);
    lFinalColour = lerp( lFinalColour, FogColourPlusWhiteLevel.rgb, float(IN.ReflectionVectorAndFog.w) );
#ifdef D_MRT
    oColour0 = float4(lFinalColour, 1);
    float lfDepth = ( IN.hPositionDepthCopy.x / IN.hPositionDepthCopy.y );
    oColour1 = ConvertDepthToARGB( lfDepth );
#else
    return float4(lFinalColour, 1);
#endif
}
technique Default
{
    pass p0
    {  
  VertexShader = compile vs_3_0 VS_Main();
  PixelShader  = compile ps_3_0 PS_Main();
    }
}
struct vertexInputZOnly {
    float3 position    : POSITION;
};
struct vertexOutputZOnly {
    float4 hPosition   : POSITION;
#ifdef D_MSAA_ENABLED
    float2 hPositionDepthCopy   : TEXCOORD0;
#endif
};
vertexOutputZOnly VS_Main_ZOnly( vertexInputZOnly IN
        ) 
{ 
    vertexOutputZOnly OUT;
     float3 WorldSpacePosition = mul( float4( IN.position, 1.0f ), world ).xyz;           
    OUT.hPosition    = TransformWorldToProjection( WorldSpacePosition );
#ifdef D_MSAA_ENABLED
    OUT.hPositionDepthCopy = OUT.hPosition.zw;
#endif
    return OUT;
}
float4 PS_Main_ZOnly( vertexOutputZOnly IN ): COLOR
{
#ifdef D_MSAA_ENABLED
#ifdef D_ADD_STENCIL
    float lfDepth = ( IN.hPositionDepthCopy.x / IN.hPositionDepthCopy.y ) + 2.0f;
#else
    float lfDepth = ( IN.hPositionDepthCopy.x / IN.hPositionDepthCopy.y );
#endif
    return lfDepth.xxxx;
#else
    return float4(1,1,1,1);
#endif
}
technique ZOnlyOpaqueSingleSided
<
  string sharedName="ZOnlyOpaqueSingleSided";
>
{
    pass p0
    {  
        CullMode = cw;
#ifndef D_MSAA_ENABLED
        ColorWriteEnable = 0;
#endif
  VertexShader = compile vs_3_0 VS_Main_ZOnly();
  PixelShader  = compile ps_3_0 PS_Main_ZOnly();
    }
}





